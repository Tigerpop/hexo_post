---
layout: posts
title: solidityå­¦ä¹ 
date: 2025-10-4 10:27:21
description: "è¿™æ˜¯æ–‡ç« å¼€å¤´ï¼Œæ˜¾ç¤ºåœ¨ä¸»é¡µé¢ï¼Œè¯¦æƒ…è¯·ç‚¹å‡»æ­¤å¤„ã€‚"
categories: 
- "åŒºå—é“¾"
tags:
- "solidity"

---



solidityæ–‡æ¡£ `https://learnblockchain.cn/docs/solidity/`

# å‡ ä¸ªå®ä¾‹

## é€‰ä¸¾

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0 <0.9.0;
/// @title å§”æ‰˜æŠ•ç¥¨
contract Ballot {
    // è¿™é‡Œå£°æ˜äº†ä¸€ä¸ªæ–°çš„å¤åˆç±»å‹ç”¨äºç¨åçš„å˜é‡ã€‚
    // è¯¥å˜é‡ç”¨æ¥è¡¨ç¤ºä¸€ä¸ªé€‰æ°‘ã€‚
    struct Voter {
        uint weight; // è®¡ç¥¨çš„æƒé‡
        bool voted;  // è‹¥ä¸ºçœŸï¼Œä»£è¡¨è¯¥äººå·²æŠ•ç¥¨
        address delegate; // è¢«å§”æ‰˜äºº
        uint vote;   // æŠ•ç¥¨ææ¡ˆçš„ç´¢å¼•
    }

    // ææ¡ˆçš„ç±»å‹.
    struct Proposal {
        bytes32 name;   // ç®€ç§°ï¼ˆæœ€é•¿32ä¸ªå­—èŠ‚ï¼‰
        uint voteCount; // å¾—ç¥¨æ•°  
    }

    address public chairperson;

    // è¿™å£°æ˜äº†ä¸€ä¸ªçŠ¶æ€å˜é‡ï¼Œä¸ºæ¯ä¸ªå¯èƒ½çš„åœ°å€å­˜å‚¨ä¸€ä¸ª `Voter`ã€‚
    mapping(address => Voter) public voters;

    // ä¸€ä¸ª `Proposal` ç»“æ„ç±»å‹çš„åŠ¨æ€æ•°ç»„
    Proposal[] public proposals;

    /// ä¸º `proposalNames` ä¸­çš„æ¯ä¸ªææ¡ˆï¼Œåˆ›å»ºä¸€ä¸ªæ–°çš„ï¼ˆæŠ•ç¥¨ï¼‰è¡¨å†³ memory è¡¨ç¤º ä»ä¸´æ—¶å†…å­˜ä¸­è¯»å–ï¼Œå°±æ˜¯ è·å–å¤–éƒ¨è¾“å…¥ã€‚ 
    constructor(bytes32[] memory proposalNames) {  // è¿™é‡Œç”¨ memory ä¸´æ—¶ç”¨ä¸€ä¸‹ï¼Œä½†æ˜¯proposals.push ä¼šè®©å†…å®¹ä¿å­˜åˆ°æ°¸ä¹… storageä¸­ã€‚ 
        chairperson = msg.sender;                  // msg.sender æ˜¯è°ƒç”¨åˆçº¦è€…ï¼ˆæ¶ˆæ¯å‘é€è€…ï¼‰è‡ªå·±çš„åœ°å€ã€‚
        voters[chairperson].weight = 1;

        //å¯¹äºæä¾›çš„æ¯ä¸ªææ¡ˆåç§°ï¼Œ
        //åˆ›å»ºä¸€ä¸ªæ–°çš„ Proposal å¯¹è±¡å¹¶æŠŠå®ƒæ·»åŠ åˆ°æ•°ç»„çš„æœ«å°¾ã€‚
        for (uint i = 0; i < proposalNames.length; i++) {
            // `Proposal({...})` åˆ›å»ºä¸€ä¸ªä¸´æ—¶ Proposal å¯¹è±¡ï¼Œ
            // `proposals.push(...)` å°†å…¶æ·»åŠ åˆ° `proposals` çš„æœ«å°¾
            proposals.push(Proposal({
                name: proposalNames[i],
                voteCount: 0
            }));
        }
    }

    // æˆæƒ `voter` å¯¹è¿™ä¸ªï¼ˆæŠ•ç¥¨ï¼‰è¡¨å†³è¿›è¡ŒæŠ•ç¥¨ã€‚
    // åªæœ‰ `chairperson` å¯ä»¥è°ƒç”¨è¯¥å‡½æ•°ã€‚
    function giveRightToVote(address voter) external {
        // è‹¥ `require` çš„ç¬¬ä¸€ä¸ªå‚æ•°çš„è®¡ç®—ç»“æœä¸º `false`ï¼Œ
        // åˆ™ç»ˆæ­¢æ‰§è¡Œï¼Œæ’¤é”€æ‰€æœ‰å¯¹çŠ¶æ€å’Œä»¥å¤ªå¸ä½™é¢çš„æ”¹åŠ¨ã€‚
        // åœ¨æ—§ç‰ˆçš„ EVM ä¸­è¿™æ›¾ç»ä¼šæ¶ˆè€—æ‰€æœ‰ gasï¼Œä½†ç°åœ¨ä¸ä¼šäº†ã€‚
        // ä½¿ç”¨ require æ¥æ£€æŸ¥å‡½æ•°æ˜¯å¦è¢«æ­£ç¡®åœ°è°ƒç”¨ï¼Œæ˜¯ä¸€ä¸ªå¥½ä¹ æƒ¯ã€‚
        // ä½ ä¹Ÿå¯ä»¥åœ¨ require çš„ç¬¬äºŒä¸ªå‚æ•°ä¸­æä¾›ä¸€ä¸ªå¯¹é”™è¯¯æƒ…å†µçš„è§£é‡Šã€‚
        require(
            msg.sender == chairperson,
            "Only chairperson can give right to vote."
        );
        require(
            !voters[voter].voted,
            "The voter already voted."
        );
        require(voters[voter].weight == 0);  // é¿å… é‡å¤æˆæƒ ï¼Œè¿™æ ·æ— æ„ä¹‰ã€‚ 
        voters[voter].weight = 1;
    }

    /// æŠŠä½ çš„æŠ•ç¥¨å§”æ‰˜åˆ°æŠ•ç¥¨è€… `to`ã€‚
    function delegate(address to) external {
        // ä¼ å¼•ç”¨
        Voter storage sender = voters[msg.sender];  //storage â†’ "ä¿®æ”¹æ°¸ä¹…çŠ¶æ€"åç»­æ“ä½œç›´æ¥ä¿®æ”¹åŒºå—é“¾çŠ¶æ€ï¼›memory â†’ "æˆ‘åªéœ€è¦ä¸´æ—¶è®¡ç®—"
        require(sender.weight != 0, "You have no right to vote");
        require(!sender.voted, "You already voted.");

        require(to != msg.sender, "Self-delegation is disallowed.");

        // å§”æ‰˜æ˜¯å¯ä»¥ä¼ é€’çš„ï¼Œåªè¦è¢«å§”æ‰˜è€… `to` ä¹Ÿè®¾ç½®äº†å§”æ‰˜ã€‚
        // ä¸€èˆ¬æ¥è¯´ï¼Œè¿™ç§å¾ªç¯å§”æ‰˜æ˜¯å±é™©çš„ã€‚å› ä¸ºï¼Œå¦‚æœä¼ é€’çš„é“¾æ¡å¤ªé•¿ï¼Œ
        // åˆ™å¯èƒ½éœ€æ¶ˆè€—çš„gasè¦å¤šäºåŒºå—ä¸­å‰©ä½™çš„ï¼ˆå¤§äºåŒºå—è®¾ç½®çš„gasLimitï¼‰ï¼Œ
        // è¿™ç§æƒ…å†µä¸‹ï¼Œå§”æ‰˜ä¸ä¼šè¢«æ‰§è¡Œã€‚
        // è€Œåœ¨å¦ä¸€äº›æƒ…å†µä¸‹ï¼Œå¦‚æœå½¢æˆé—­ç¯ï¼Œåˆ™ä¼šè®©åˆçº¦å®Œå…¨å¡ä½ã€‚
        while (voters[to].delegate != address(0)) {  // æ‰¾åˆ° å§”æ‰˜é“¾çš„æœ€åä¸€ä¸ªå§”æ‰˜è€…ï¼Œaddress(0)æŒ‡çš„æ˜¯ nullç±»ä¼¼äºå§”æ‰˜é“¾çš„ç»ˆç‚¹ã€‚
            to = voters[to].delegate;

            // ä¸å…è®¸é—­ç¯å§”æ‰˜
            require(to != msg.sender, "Found loop in delegation.");
        }

        Voter storage delegate_ = voters[to];      // æœ€ç»ˆå§”æ‰˜è€… ï¼ˆæœ€ç»ˆæŠ•ç¥¨è€…ï¼‰

        // Voters cannot delegate to accounts that cannot vote.
        require(delegate_.weight >= 1);

        // Since `sender` is a reference, this
        // modifies `voters[msg.sender]`.
        sender.voted = true;
        sender.delegate = to;

        if (delegate_.voted) {
            // è‹¥è¢«å§”æ‰˜è€…å·²ç»æŠ•è¿‡ç¥¨äº†ï¼Œç›´æ¥å¢åŠ å¾—ç¥¨æ•°
            proposals[delegate_.vote].voteCount += sender.weight;
        } else {
            // è‹¥è¢«å§”æ‰˜è€…è¿˜æ²¡æŠ•ç¥¨ï¼Œå¢åŠ å§”æ‰˜è€…çš„æƒé‡
            delegate_.weight += sender.weight;
        }
    }

    /// æŠŠä½ çš„ç¥¨(åŒ…æ‹¬å§”æ‰˜ç»™ä½ çš„ç¥¨)ï¼Œ
    /// æŠ•ç»™ææ¡ˆ `proposals[proposal].name`.
    function vote(uint proposal) external {
        Voter storage sender = voters[msg.sender];
        require(sender.weight != 0, "Has no right to vote");
        require(!sender.voted, "Already voted.");
        sender.voted = true;
        sender.vote = proposal;

        // å¦‚æœ `proposal` è¶…è¿‡äº†æ•°ç»„çš„èŒƒå›´ï¼Œåˆ™ä¼šè‡ªåŠ¨æŠ›å‡ºå¼‚å¸¸ï¼Œå¹¶æ¢å¤æ‰€æœ‰çš„æ”¹åŠ¨

        proposals[proposal].voteCount += sender.weight;
    }

    /// @dev ç»“åˆä¹‹å‰æ‰€æœ‰çš„æŠ•ç¥¨ï¼Œè®¡ç®—å‡ºæœ€ç»ˆèƒœå‡ºçš„ææ¡ˆ ï¼Œview åªè¯»ä¸ä¿®æ”¹çŠ¶æ€ï¼Œå…gasè´¹ã€‚
    function winningProposal() public view  
            returns (uint winningProposal_)    
    {
        uint winningVoteCount = 0;
        for (uint p = 0; p < proposals.length; p++) {
            if (proposals[p].voteCount > winningVoteCount) {
                winningVoteCount = proposals[p].voteCount;
                winningProposal_ = p;
            }
        }
    }
    // è°ƒç”¨ winningProposal() å‡½æ•°ä»¥è·å–ææ¡ˆæ•°ç»„ä¸­è·èƒœè€…çš„ç´¢å¼•ï¼Œå¹¶ä»¥æ­¤è¿”å›è·èƒœè€…çš„åç§°
    function winnerName() external view
            returns (bytes32 winnerName_)
    {
        winnerName_ = proposals[winningProposal()].name;
    }
}
```

## æ™®é€šç«æ‹

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;
contract SimpleAuction {
    // æ‹å–çš„å‚æ•°ã€‚æ—¶é—´å¯ä»¥æ˜¯ç»å¯¹çš„ unix æ—¶é—´æˆ³ï¼ˆè‡ª 1970-01-01 èµ·çš„ç§’æ•°ï¼‰æˆ–ä»¥ç§’ä¸ºå•ä½çš„æ—¶é—´æ®µã€‚
    address payable public beneficiary;  // æ ‡è®°è¯¥åœ°å€èƒ½å¤Ÿæ¥æ”¶èµ„é‡‘
    uint public auctionEndTime;

    // æ‹å–çš„å½“å‰çŠ¶æ€ã€‚
    address public highestBidder;
    uint public highestBid;

    // å…è®¸å–å›çš„å…ˆå‰å‡ºä»·
    mapping(address => uint) pendingReturns;

    // åœ¨ç»“æŸæ—¶è®¾ç½®ä¸º trueï¼Œç¦æ­¢ä»»ä½•æ›´æ”¹ã€‚
    // é»˜è®¤åˆå§‹åŒ–ä¸º `false`ã€‚
    bool ended;

    // å˜æ›´è§¦å‘çš„äº‹ä»¶ã€‚ äº‹ä»¶å°±æ˜¯åŒºå—é“¾çš„"å¹¿æ’­ç³»ç»Ÿ"ï¼Œå¹¿æ’­å†…å®¹ä¸ºå‚æ•°å†…å®¹ã€‚
    event HighestBidIncreased(address bidder, uint amount);
    event AuctionEnded(address winner, uint amount);

    // Errors ç”¨æ¥å®šä¹‰å¤±è´¥

    // ä¸‰æ–œæ æ³¨é‡Šæ˜¯æ‰€è°“çš„ natspec æ³¨é‡Šã€‚
    // å½“ç”¨æˆ·è¢«è¦æ±‚ç¡®è®¤äº¤æ˜“æ—¶å°†æ˜¾ç¤ºå®ƒä»¬ï¼Œæˆ–è€…å½“æ˜¾ç¤ºé”™è¯¯æ—¶ã€‚

    /// æ‹å–å·²ç»ç»“æŸã€‚
    error AuctionAlreadyEnded();  // revert äº¤æ˜“å›æ»šâ€‹ gas é€€å›ï¼›
    /// å·²ç»æœ‰æ›´é«˜æˆ–ç›¸ç­‰çš„å‡ºä»·ã€‚
    error BidNotHighEnough(uint highestBid);
    /// æ‹å–å°šæœªç»“æŸã€‚
    error AuctionNotYetEnded();
    /// å‡½æ•° auctionEnd å·²ç»è¢«è°ƒç”¨ã€‚
    error AuctionEndAlreadyCalled();

    /// åˆ›å»ºä¸€ä¸ªç®€å•çš„æ‹å–ï¼Œæ‹å–æ—¶é—´ä¸º `biddingTime`ç§’ï¼Œä»£è¡¨å—ç›Šäººåœ°å€ `beneficiaryAddress`ã€‚
    constructor(
        uint biddingTime,
        address payable beneficiaryAddress
    ) {
        beneficiary = beneficiaryAddress;
        auctionEndTime = block.timestamp + biddingTime;
    }

    /// åœ¨æ‹å–ä¸­å‡ºä»·ï¼Œå‡ºä»·çš„å€¼ä¸æ­¤äº¤æ˜“ä¸€èµ·å‘é€ã€‚
    /// è¯¥å€¼ä»…åœ¨æ‹å–æœªè·èƒœæ—¶é€€æ¬¾ã€‚
    function bid() external payable {
        // ä¸éœ€è¦å‚æ•°ï¼Œæ‰€æœ‰ä¿¡æ¯å·²ç»æ˜¯äº¤æ˜“çš„ä¸€éƒ¨åˆ†ã€‚
        // å…³é”®å­— payable æ˜¯å¿…éœ€çš„ï¼Œä»¥ä¾¿å‡½æ•°èƒ½å¤Ÿæ¥æ”¶ä»¥å¤ªã€‚

        // å¦‚æœæ‹å–æ—¶é—´å·²è¿‡ï¼Œåˆ™æ’¤é”€è°ƒç”¨ã€‚
        if (block.timestamp > auctionEndTime)
            revert AuctionAlreadyEnded();

        // å¦‚æœå‡ºä»·ä¸é«˜ï¼Œåˆ™å°†ä»¥å¤ªå¸é€€å›ï¼ˆæ’¤é”€è¯­å¥å°†æ’¤é”€æ­¤å‡½æ•°æ‰§è¡Œä¸­çš„æ‰€æœ‰æ›´æ”¹ï¼ŒåŒ…æ‹¬å®ƒå·²æ¥æ”¶ä»¥å¤ªå¸ï¼‰ã€‚
        if (msg.value <= highestBid)
            revert BidNotHighEnough(highestBid);

        if (highestBid != 0) {
            // é€šè¿‡ç®€å•ä½¿ç”¨ highestBidder.send(highestBid) é€€å›ä»¥å¤ªå¸æ˜¯ä¸€ä¸ªå®‰å…¨é£é™©ï¼Œå› ä¸ºå®ƒå¯èƒ½ä¼šæ‰§è¡Œä¸€ä¸ªä¸å—ä¿¡ä»»çš„åˆçº¦ã€‚
            // è®©æ¥æ”¶è€…è‡ªè¡Œæå–ä»–ä»¬çš„ä»¥å¤ªå¸æ€»æ˜¯æ›´å®‰å…¨ã€‚
            pendingReturns[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;
        emit HighestBidIncreased(msg.sender, msg.value);
    }

    /// å–å›å‡ºä»·ï¼ˆå½“è¯¥å‡ºä»·å·²è¢«è¶…è¶Šï¼‰
    function withdraw() external returns (bool) {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // å°†å…¶è®¾ç½®ä¸ºé›¶å¾ˆé‡è¦ï¼Œå› ä¸ºæ¥æ”¶è€…å¯ä»¥åœ¨ `send` è¿”å›ä¹‹å‰å†æ¬¡è°ƒç”¨æ­¤å‡½æ•°ä½œä¸ºæ¥æ”¶è°ƒç”¨çš„ä¸€éƒ¨åˆ†ã€‚
            pendingReturns[msg.sender] = 0;

            // msg.sender ä¸æ˜¯ `address payable` ç±»å‹ï¼Œå¿…é¡»æ˜¾å¼è½¬æ¢ä¸º `payable(msg.sender)` ä»¥ä¾¿ä½¿ç”¨æˆå‘˜å‡½æ•° `send()`ã€‚
            if (!payable(msg.sender).send(amount)) {
                // è¿™é‡Œä¸éœ€è¦è°ƒç”¨ throwï¼Œåªéœ€é‡ç½®æœªä»˜æ¬¾
                pendingReturns[msg.sender] = amount;
                return false;
            }
        }
        return true;
    }

    /// ç»“æŸæ‹å–å¹¶å°†æœ€é«˜å‡ºä»·å‘é€ç»™å—ç›Šäººã€‚
    function auctionEnd() external {
        // è¿™æ˜¯ä¸€ä¸ªå¥½çš„æŒ‡å¯¼åŸåˆ™ï¼Œå°†ä¸å…¶ä»–åˆçº¦äº¤äº’çš„å‡½æ•°ï¼ˆå³å®ƒä»¬è°ƒç”¨å‡½æ•°æˆ–å‘é€ä»¥å¤ªï¼‰ç»“æ„åŒ–ä¸ºä¸‰ä¸ªé˜¶æ®µï¼š
        // 1. æ£€æŸ¥æ¡ä»¶
        // 2. æ‰§è¡Œæ“ä½œï¼ˆå¯èƒ½æ›´æ”¹æ¡ä»¶ï¼‰
        // 3. ä¸å…¶ä»–åˆçº¦äº¤äº’
        // å¦‚æœè¿™äº›é˜¶æ®µæ··åˆåœ¨ä¸€èµ·ï¼Œå…¶ä»–åˆçº¦å¯èƒ½ä¼šå›è°ƒå½“å‰åˆçº¦å¹¶ä¿®æ”¹çŠ¶æ€æˆ–å¯¼è‡´æ•ˆæœï¼ˆä»¥å¤ªæ”¯ä»˜ï¼‰è¢«å¤šæ¬¡æ‰§è¡Œã€‚
        // å¦‚æœå†…éƒ¨è°ƒç”¨çš„å‡½æ•°åŒ…æ‹¬ä¸å¤–éƒ¨åˆçº¦çš„äº¤äº’ï¼Œå®ƒä»¬ä¹Ÿå¿…é¡»è¢«è§†ä¸ºä¸å¤–éƒ¨åˆçº¦çš„äº¤äº’ã€‚

        // 1. æ¡ä»¶
        if (block.timestamp < auctionEndTime)
            revert AuctionNotYetEnded();
        if (ended)
            revert AuctionEndAlreadyCalled();

        // 2. ç”Ÿæ•ˆ
        ended = true;
        emit AuctionEnded(highestBidder, highestBid);

        // 3. äº¤äº’
        beneficiary.transfer(highestBid);
    }
}
```

## **"æ£€æŸ¥-ç”Ÿæ•ˆ-äº¤äº’"**åŸåˆ™

checkã€effectã€interactionã€‚

å¦‚æœè®© interactionäº¤äº’ å‘ç”Ÿåœ¨ effectçŠ¶æ€å˜åŒ–å‰é¢ï¼Œå¯èƒ½å¯¼è‡´**é‡å…¥æ”»å‡»**ã€‚

**é‡å…¥æ”»å‡»**å°±æ˜¯åˆ«äººåå¤è°ƒç”¨ä½ çš„ä¸€ä¸ªå‡½æ•°æˆ–è€…æ–¹æ³•ï¼Œå®ç°æ”»å‡»çš„æ–¹æ³•ã€‚ ä¾‹å¦‚ï¼š

```solidity
if (highestBid != 0) {
    // æ”¹ä¸ºç›´æ¥è½¬è´¦ï¼ˆæ˜“å—æ”»å‡»çš„ç‰ˆæœ¬ï¼‰
    (bool success, ) = highestBidder.call{value: highestBid}("");
    require(success, "Transfer failed");
}
highestBidder = msg.sender;
highestBid = msg.value;
emit HighestBidIncreased(msg.sender, msg.value);
```

interactionäº¤äº’ï¼ˆå‘è¿™ä¸ªåœ°å€ä¸­è½¬è´¦å…ˆå‘ç”Ÿï¼‰

```solidity
(bool success, ) = highestBidder.call{value: highestBid}("");
```

effectçŠ¶æ€åæ”¹å˜

```solidity
highestBidder = msg.sender;
highestBid = msg.value;
```

å‡å¦‚æ­¤æ—¶æœ‰ä¸€ä¸ª é»‘å®¢åˆçº¦ï¼Œï¼ˆåœ¨è¢«åˆ«çš„åˆçº¦callè°ƒç”¨ æ”¶æ¬¾æ—¶ï¼Œä¼šè‡ªåŠ¨ è¿è¡Œreceiveå‡½æ•°ï¼Œè¿™æ˜¯åŒºå—é“¾è§„åˆ™ï¼‰ï¼Œ

```solidity

receive() external payable {
    // åœ¨æ¥æ”¶ä»¥å¤ªå¸æ—¶ï¼Œé‡æ–°è°ƒç”¨ bid å‡½æ•°ï¼Œå‡ºä»·ç•¥é«˜äºå½“å‰æœ€é«˜ä»·
    auction.bid{value: currentHighestBid + 1 wei}();
}
```

å®ƒå…ˆå‡ºä»·1ethï¼Œåˆ«äººè¿›ä»·2ethï¼Œå®ƒå°±ä¼šä»¥ä¹‹å‰çš„æœ€é«˜ä»·è¢«è°ƒç”¨`highestBidder.call{value: highestBid}("")`ï¼Œå®ƒåœ¨æ”¶åˆ°1ethé€€æ¬¾åï¼Œæ¥äº†ä¸€ä¸ª é‡æ–°è°ƒç”¨ bid å‡½æ•°çš„æ“ä½œï¼Œè€Œä¸”å‡ºä»·ä»…ä»…æ¯”ä¹‹å‰é«˜ä¸€ç‚¹ç‚¹å¦‚1.001ethã€‚ç”±äºä¹‹å‰çš„  effectçŠ¶æ€æ”¹å˜è¿˜æœªå‘ç”Ÿï¼Œå¯¼è‡´è¿˜æ˜¯ä¹‹å‰è‡ªå·±è€çš„å‡ºä»· 1ethä½œä¸º `highestBid` ï¼Œè‡ªå·±è¿™é«˜ä¸€ç‚¹ç‚¹çš„ æ–°ç«ä»·ï¼Œèƒ½å¤Ÿé€šè¿‡ bidä¸­ check æ­¥éª¤ `if (msg.value <= highestBid)` ç­‰ç­‰è¿™äº›æ¡ä»¶ï¼Œç„¶å å†åº¦åˆ·æ–°è‡ªå·±ä¹‹å‰ æœ€é«˜ç«ä»·é€€æ¬¾çš„æ­¥éª¤`highestBidder.call{value: highestBid}("")`,å†æ¬¡æ”¶åˆ° 1ethçš„é€€æ¬¾ï¼Œè€Œå†æ¬¡åœ¨ é»‘å®¢åˆçº¦ä¸­è°ƒç”¨receiveå‡½æ•°ï¼Œå†æ¬¡æ”¶åˆ° 1ethçš„é€€æ¬¾ï¼Œå®ç°é€’å½’ï¼Œç›´åˆ°èŠ±å…‰gas æˆ–è€…åœ¨è‡ªå·±å†™çš„æŸä¸ªæ¡ä»¶åœä¸‹æ¥ã€‚ 

## ç›²æ‹

**å…·æœ‰çº¦æŸåŠ›ä¸”ä¿å¯†**ï¼šé˜²æ­¢ç«æ ‡è€…åœ¨èµ¢å¾—æ‹å–åä¸å‘é€ä»¥å¤ªå¸çš„å”¯ä¸€æ–¹æ³•æ˜¯è®©ä»–ä»¬åœ¨å‡ºä»·æ—¶ä¸€èµ·å‘é€ï¼Œå“ˆå¸Œå®ç°ä¿å¯†ã€‚

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;
contract BlindAuction {
    struct Bid {
        bytes32 blindedBid;
        uint deposit;
    }

    address payable public beneficiary;
    uint public biddingEnd;
    uint public revealEnd;
    bool public ended;

    mapping(address => Bid[]) public bids;

    address public highestBidder;
    uint public highestBid;

    // å…è®¸æå–ä¹‹å‰å‡ºä»·
    mapping(address => uint) pendingReturns;

    event AuctionEnded(address winner, uint highestBid);

    // Errors ç”¨æ¥å®šä¹‰å¤±è´¥

    /// å‡½æ•°è¢«è°ƒç”¨å¾—å¤ªæ—©ã€‚
    /// è¯·åœ¨ `time` å†è¯•ä¸€æ¬¡ã€‚
    error TooEarly(uint time);
    /// å‡½æ•°è¢«è°ƒç”¨å¾—å¤ªæ™šã€‚
    /// ä¸èƒ½åœ¨ `time` ä¹‹åè°ƒç”¨ã€‚
    error TooLate(uint time);
    /// å‡½æ•° auctionEnd å·²ç»è¢«è°ƒç”¨ã€‚
    error AuctionEndAlreadyCalled();

    // ä¿®æ”¹å™¨æ˜¯ä¸€ç§æ–¹ä¾¿çš„æ–¹å¼æ¥éªŒè¯è¾“å…¥å‡½æ•°ã€‚
    // `onlyBefore` åº”ç”¨äºä¸‹é¢çš„ `bid`ï¼šæ–°çš„å‡½æ•°ä½“æ˜¯ä¿®æ”¹å™¨çš„ä¸»ä½“ï¼Œå…¶ä¸­ `_` è¢«æ—§å‡½æ•°ä½“æ›¿æ¢ã€‚
    // solidity ä¸­çš„ modifier æ–¹æ³• å¾ˆåƒ pythonä¸­çš„ @ åé¢çš„è£…é¥°å™¨å•Šï¼Œå°±æ˜¯ä¸€ä¸ª å¯¹è¢«ä¿®é¥°æ–¹æ³•çš„é—­åŒ…å¼çš„ å°è£…ã€‚ 
    modifier onlyBefore(uint time) {
        if (block.timestamp >= time) revert TooLate(time);
        _;
    }
    modifier onlyAfter(uint time) {
        if (block.timestamp <= time) revert TooEarly(time);
        _;
    }

    constructor(
        uint biddingTime,
        uint revealTime,
        address payable beneficiaryAddress
    ) {
        beneficiary = beneficiaryAddress;
        biddingEnd = block.timestamp + biddingTime;
        revealEnd = biddingEnd + revealTime;
    }

    /// ä»¥ `blindedBid` = keccak256(abi.encodePacked(value, fake, secret)) çš„æ–¹å¼æäº¤ä¸€ä¸ªç›²å‡ºä»·ï¼Œå®ƒæ˜¯ä¸€ä¸ªhashå€¼ã€‚
    /// å‘é€çš„ä»¥å¤ªå¸ä»…åœ¨å‡ºä»·åœ¨æ­ç¤ºé˜¶æ®µè¢«æ­£ç¡®æ­ç¤ºæ—¶æ‰ä¼šé€€è¿˜ã€‚
    /// å¦‚æœä¸å‡ºä»·ä¸€èµ·å‘é€çš„ä»¥å¤ªå¸è‡³å°‘ä¸º "value" ä¸” "fake" ä¸ä¸ºçœŸï¼Œåˆ™å‡ºä»·æœ‰æ•ˆã€‚
    /// å°† "fake" è®¾ç½®ä¸ºçœŸå¹¶å‘é€ä¸å‡†ç¡®çš„é‡‘é¢æ˜¯éšè—çœŸå®å‡ºä»·çš„æ–¹å¼ï¼Œä½†ä»ç„¶æ»¡è¶³æ‰€éœ€çš„å­˜æ¬¾ã€‚
    /// ç›¸åŒåœ°å€å¯ä»¥æäº¤å¤šä¸ªå‡ºä»·ã€‚
    // onlyBefore(biddingEnd) å°±æ˜¯ solidityä½¿ç”¨å‰æ–‡è£…é¥°å™¨çš„å†™æ³•ã€‚ 
    function bid(bytes32 blindedBid)
        external
        payable
        onlyBefore(biddingEnd)
    {
        bids[msg.sender].push(Bid({
            blindedBid: blindedBid,
            deposit: msg.value
        }));
    }

    /// ç”¨æˆ·è‡ªå·±æ¥æ­ç¤ºç›²å‡ºä»·ã€‚
    /// å°†è·å¾—æ‰€æœ‰æ­£ç¡®ç›²å‡ºçš„æ— æ•ˆå‡ºä»·çš„é€€æ¬¾ï¼Œä»¥åŠé™¤äº†æœ€é«˜å‡ºä»·ä¹‹å¤–çš„æ‰€æœ‰å‡ºä»·ã€‚
    // åœ¨ Solidity ä¸­ï¼Œå‡½æ•°å‚æ•°æœ‰ä¸‰ç§å­˜å‚¨ä½ç½®ï¼š
    // memory- æ˜“å¤±æ€§å†…å­˜ï¼ˆå¯ä¿®æ”¹ï¼‰
    // storage- æ°¸ä¹…å­˜å‚¨ï¼ˆå¯ä¿®æ”¹ï¼‰
    // calldata- åªè¯»è°ƒç”¨æ•°æ®ï¼ˆä¸å¯ä¿®æ”¹ï¼‰
    function reveal(
        uint[] calldata values,
        bool[] calldata fakes,
        bytes32[] calldata secrets
    )
        external
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        require(values.length == length);
        require(fakes.length == length);
        require(secrets.length == length);

        uint refund; // é€€æ¬¾
        for (uint i = 0; i < length; i++) {
            Bid storage bidToCheck = bids[msg.sender][i]; // å…¬ç¤ºæ•°æ®æ˜¯é“¾ä¸Šè¦è®°å½•çš„ã€‚
            // è·å–ç”¨æˆ·æäº¤çš„æ­ç¤ºæ•°æ®
            (uint value, bool fake, bytes32 secret) =
                    (values[i], fakes[i], secrets[i]);
            if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                // å‡ºä»·æœªèƒ½æ­£ç¡®æŠ«éœ²
                // ä¸é€€è¿˜å­˜æ¬¾ã€‚
                continue;
            }
            refund += bidToCheck.deposit;
            // å¦‚æœæ˜¯çœŸå®å‡ºä»·ä¸”æŠ¼é‡‘>=å‡ºä»·é‡‘é¢
            if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            // ä½¿å‘é€è€…æ— æ³•é‡æ–°å–å›ç›¸åŒçš„å­˜æ¬¾ã€‚å¦‚æœæ²¡æœ‰è¿™ä¸€æ­¥ï¼Œç”¨æˆ·å†è°ƒç”¨ revealå‡½æ•°ï¼Œåˆä¼šé€€ä¸€éï¼Œè¿™ä¸€æ­¥çš„ç›®çš„ç›¸å½“äºæ’¤é”€æ‰¿è¯ºã€‚
            bidToCheck.blindedBid = bytes32(0);
        }
        payable(msg.sender).transfer(refund);
    }

    /// æå–è¢«è¶…å‡ºå‡ºä»·çš„å‡ºä»·ã€‚
    function withdraw() external {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // å°†å…¶è®¾ç½®ä¸ºé›¶æ˜¯é‡è¦çš„ï¼Œ
            // å› ä¸ºï¼Œä½œä¸ºæ¥æ”¶è°ƒç”¨çš„ä¸€éƒ¨åˆ†ï¼Œ
            // æ¥æ”¶è€…å¯ä»¥åœ¨ `transfer` è¿”å›ä¹‹å‰é‡æ–°è°ƒç”¨è¯¥å‡½æ•°ã€‚ï¼ˆå¯æŸ¥çœ‹ä¸Šé¢å…³äºâ€œæ¡ä»¶ -> ç”Ÿæ•ˆ -> äº¤äº’â€çš„æ ‡æ³¨ï¼‰
            pendingReturns[msg.sender] = 0;

            payable(msg.sender).transfer(amount);
        }
    }

    /// ç»“æŸæ‹å–å¹¶å°†æœ€é«˜å‡ºä»·å‘é€ç»™å—ç›Šäººã€‚
    function auctionEnd()
        external
        onlyAfter(revealEnd)
    {
        if (ended) revert AuctionEndAlreadyCalled(); // ended åœ¨ä¸€å¼€å§‹ é»˜è®¤æ˜¯0 ä¹Ÿå°±æ˜¯ false é»˜è®¤æ²¡æœ‰ç»“æŸã€‚
        emit AuctionEnded(highestBidder, highestBid);
        ended = true;                                // é”ä½äº†ã€‚ä¸èƒ½åå¤ ç»™å—ç›Šäººæ‰“é’±ã€‚
        beneficiary.transfer(highestBid);
    }

    // è¿™æ˜¯ä¸€ä¸ªâ€œå†…éƒ¨â€å‡½æ•°ï¼Œè¿™æ„å‘³ç€å®ƒåªèƒ½ä»åˆçº¦æœ¬èº«ï¼ˆæˆ–ä»æ´¾ç”Ÿåˆçº¦ï¼‰è°ƒç”¨ã€‚
    function placeBid(address bidder, uint value) internal
            returns (bool success)
    {
        if (value <= highestBid) {
            return false;
        }
        if (highestBidder != address(0)) {
            // é€€æ¬¾ç»™ä¹‹å‰çš„æœ€é«˜å‡ºä»·è€…ã€‚
            pendingReturns[highestBidder] += highestBid;
        }
        highestBid = value;
        highestBidder = bidder;
        return true;
    }
}
```

## è¿œç¨‹è´­ä¹°
åŒæ–¹å¿…é¡»å°†ç‰©å“ä»·å€¼çš„ä¸¤å€æ”¾å…¥åˆçº¦ä½œä¸ºæ‰˜ç®¡ã€‚ ä¸€æ—¦å‘ç”ŸçŠ¶å†µï¼Œä»¥å¤ªå¸å°†è¢«é”å®šåœ¨åˆçº¦ä¸­ï¼Œç›´åˆ°ä¹°æ–¹ç¡®è®¤ä»–ä»¬æ”¶åˆ°äº†ç‰©å“ã€‚ ä¹‹åï¼Œä¹°æ–¹å°†è·å¾—ä»·å€¼ï¼ˆä»–ä»¬å­˜æ¬¾çš„ä¸€åŠï¼‰ï¼Œè€Œå–æ–¹å°†è·å¾—ä¸‰å€çš„ä»·å€¼ï¼ˆä»–ä»¬çš„å­˜æ¬¾åŠ ä¸Šä»·å€¼ï¼‰ã€‚ å…¶èƒŒåçš„æƒ³æ³•æ˜¯åŒæ–¹éƒ½æœ‰åŠ¨åŠ›æ¥è§£å†³è¿™ç§æƒ…å†µï¼Œå¦åˆ™ä»–ä»¬çš„ä»¥å¤ªå¸å°†æ°¸è¿œè¢«é”å®šã€‚

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.4;
contract Purchase {
    uint public value;
    address payable public seller;
    address payable public buyer;

    enum State { Created, Locked, Release, Inactive } // å®šä¹‰ä¸€ä¸ªæšä¸¾ç±»
    // çŠ¶æ€å˜é‡çš„é»˜è®¤å€¼ä¸ºç¬¬ä¸€ä¸ªæˆå‘˜ï¼Œ`State.created`
    State public state;

    modifier condition(bool condition_) {
        require(condition_);
        _;
    }

    /// åªæœ‰ä¹°æ–¹å¯ä»¥è°ƒç”¨æ­¤å‡½æ•°ã€‚
    error OnlyBuyer();
    /// åªæœ‰å–æ–¹å¯ä»¥è°ƒç”¨æ­¤å‡½æ•°ã€‚
    error OnlySeller();
    /// å½“å‰çŠ¶æ€ä¸‹æ— æ³•è°ƒç”¨è¯¥å‡½æ•°ã€‚
    error InvalidState();
    /// æä¾›çš„å€¼å¿…é¡»æ˜¯å¶æ•°ã€‚
    error ValueNotEven();

    modifier onlyBuyer() {
        if (msg.sender != buyer)
            revert OnlyBuyer();
        _;
    }

    modifier onlySeller() {
        if (msg.sender != seller)
            revert OnlySeller();
        _;
    }

    modifier inState(State state_) {
        if (state != state_)
            revert InvalidState();
        _;
    }

    event Aborted();
    event PurchaseConfirmed();
    event ItemReceived();
    event SellerRefunded();

    // ç¡®ä¿ `msg.value` æ˜¯ä¸€ä¸ªå¶æ•°ã€‚
    // å¦‚æœæ˜¯å¥‡æ•°ï¼Œé™¤æ³•å°†æˆªæ–­ã€‚
    // é€šè¿‡ä¹˜æ³•æ£€æŸ¥å®ƒä¸æ˜¯å¥‡æ•°ã€‚
    constructor() payable {
        seller = payable(msg.sender);
        value = msg.value / 2;
        if ((2 * value) != msg.value)
            revert ValueNotEven();
    }

    /// ä¸­æ­¢è´­ä¹°å¹¶æ”¶å›ä»¥å¤ªå¸ã€‚
    /// åªèƒ½ç”±å–æ–¹åœ¨åˆçº¦è¢«é”å®šä¹‹å‰è°ƒç”¨ã€‚
    function abort()
        external
        onlySeller
        inState(State.Created)
    {
        emit Aborted();
        state = State.Inactive;
        // æˆ‘ä»¬åœ¨è¿™é‡Œç›´æ¥ä½¿ç”¨è½¬è´¦ã€‚
        // å¯ç”¨äºé˜²æ­¢é‡å…¥ï¼Œå› ä¸ºå®ƒæ˜¯æ­¤å‡½æ•°ä¸­çš„æœ€åä¸€ä¸ªè°ƒç”¨ï¼Œæˆ‘ä»¬å·²ç»æ”¹å˜äº†çŠ¶æ€ã€‚
        seller.transfer(address(this).balance);
    }

    /// ä½œä¸ºä¹°æ–¹ç¡®è®¤è´­ä¹°ã€‚
    /// äº¤æ˜“å¿…é¡»åŒ…æ‹¬ `2 * value` ä»¥å¤ªå¸ã€‚
    /// ä»¥å¤ªå¸å°†åœ¨è°ƒç”¨ confirmReceived ä¹‹å‰è¢«é”å®šã€‚
    function confirmPurchase()
        external
        inState(State.Created)
        condition(msg.value == (2 * value))
        payable
    {
        emit PurchaseConfirmed();
        buyer = payable(msg.sender);
        state = State.Locked;
    }

    /// ç¡®è®¤ä½ ï¼ˆä¹°æ–¹ï¼‰æ”¶åˆ°äº†ç‰©å“ã€‚
    /// è¿™å°†é‡Šæ”¾é”å®šçš„ä»¥å¤ªå¸ã€‚
    function confirmReceived()
        external
        onlyBuyer
        inState(State.Locked)
    {
        emit ItemReceived();
        // é¦–å…ˆæ”¹å˜çŠ¶æ€æ˜¯å¾ˆé‡è¦çš„ï¼Œ
        // å¦åˆ™ï¼Œä½¿ç”¨ `send` è°ƒç”¨çš„åˆçº¦å¯ä»¥å†æ¬¡è°ƒç”¨è¿™é‡Œã€‚
        state = State.Release;

        buyer.transfer(value);
    }

    /// æ­¤å‡½æ•°é€€æ¬¾ç»™å–æ–¹ï¼Œå³é€€è¿˜å–æ–¹çš„é”å®šèµ„é‡‘ã€‚
    function refundSeller()
        external
        onlySeller
        inState(State.Release)
    {
        emit SellerRefunded();
        // é¦–å…ˆæ”¹å˜çŠ¶æ€æ˜¯å¾ˆé‡è¦çš„ï¼Œ
        // å¦åˆ™ï¼Œä½¿ç”¨ `send` è°ƒç”¨çš„åˆçº¦å¯ä»¥å†æ¬¡è°ƒç”¨è¿™é‡Œã€‚
        state = State.Inactive;

        seller.transfer(3 * value);
    }
}
```

## å¾®æ”¯ä»˜é€šé“

å¯¹äºæ²¡æœ‰æ¥è§¦è¿‡åŒºå—é“¾çš„äººæ¥è¯´åº”è¯¥å…ˆçœ‹çœ‹ä¸‹é¢è¿™ä¸ªé€šä¿—çš„è§£é‡Šæ¥ç†è§£ä¸€ä¸‹ ã€å¾®æ”¯ä»˜é€šé“ã€‘çš„æ„æ€ï¼›

### ï¼ˆä¸€ï¼‰é€šä¿—çš„ä¾‹å­ï¼ˆæ–°äººå¿…çœ‹ï¼‰

æ¯”å–»åœºæ™¯ï¼šä½ å»å’–å•¡åº—å–å’–å•¡ï¼ˆå¾®æ”¯ä»˜é€šé“ï¼‰

ä½ æ¯å¤©éƒ½å»åŒä¸€å®¶å’–å•¡åº—ï¼Œç‚¹å’–å•¡ä»·æ ¼æ˜¯ **10 å…ƒ**ã€‚

ä½†æ˜¯â€”â€”æ¯æ¬¡éƒ½ç”¨åŒºå—é“¾æ”¯ä»˜å¤ªæ…¢ã€å¤ªè´µï¼ˆGasï¼‰ã€‚
 æ‰€ä»¥ä½ å’Œåº—ä¸»æƒ³å‡ºä¸€ä¸ªåŠæ³•ï¼š**å…ˆå¼€ä¸€ä¸ªâ€œè®°è´¦æœ¬â€**ï¼ˆé€šé“ï¼‰ï¼Œåªåœ¨å¼€å§‹å’Œç»“æŸæ—¶ä¸Šé“¾ï¼Œä¸­é—´çš„æ¯ä¸€æ¯å’–å•¡éƒ½ç¦»çº¿ç»“ç®—ã€‚

------

#### ğŸª™ ç¬¬ä¸€æ­¥ï¼šå¼€é€šé€šé“ï¼ˆä¸Šé“¾ï¼‰

ä½ å’Œå’–å•¡åº—è€æ¿åœ¨åŒºå—é“¾ä¸Šç­¾äº†ä¸€ä¸ªâ€œåˆçº¦â€ï¼š

> ã€Œæˆ‘å…ˆåœ¨åˆçº¦é‡Œå­˜ 100 å…ƒï¼Œä»£è¡¨æˆ‘æœªæ¥æœ€å¤šèƒ½å– 10 æ¯å’–å•¡ã€‚ã€

è¿™ç¬” 100 å…ƒæ˜¯**æŠ¼é‡‘**ï¼Œå†™è¿›åŒºå—é“¾é‡Œã€‚

------

#### âœï¸ ç¬¬äºŒæ­¥ï¼šç¦»çº¿ç­¾åï¼ˆç¦»é“¾æ”¯ä»˜ï¼‰

ä½ å–ç¬¬ä¸€æ¯å’–å•¡ã€‚
 è€æ¿è¦ä½ â€œç­¾ä¸ªå­—â€è¡¨ç¤ºã€Œæˆ‘æ‰¿è®¤è¿™æ¯å’–å•¡èŠ±äº† 10 å…ƒã€ã€‚

äºæ˜¯ä½ å†™äº†ä¸€å¼ **ç­¾ç½²çš„å‡­è¯**ï¼š

```
æˆ‘ï¼ˆå®¢æˆ·ï¼‰åŒæ„æ”¯ä»˜è€æ¿ 10 å…ƒã€‚
ç­¾åï¼šYushao
```

> è¿™å¼ â€œç­¾åå‡­è¯â€ä¸éœ€è¦ä¸Šé“¾ï¼Œåªå­˜åœ¨ä½ å’Œè€æ¿ä¹‹é—´ã€‚

å–ç¬¬äºŒæ¯æ—¶ï¼Œä½ å†å†™ï¼š

```
æˆ‘åŒæ„æ”¯ä»˜è€æ¿ 20 å…ƒã€‚
ç­¾åï¼šYushao
```

è¡¨ç¤ºï¼š**æˆªæ­¢ç›®å‰**æˆ‘æ¬ ä»– 20 å…ƒï¼ˆç´¯è®¡çš„ï¼‰ã€‚

> å¦‚æœè¦åŠ ä¸Šä¸€äº›å¤æ‚çš„å‚æ•°æ¥ç»„è£…å‡ºç­¾åçš„å†…å®¹

> ```markdown
> æˆ‘æ¬  è€æ¿å¼ ä¸‰ï¼ˆrecipientï¼‰çš„ é’±ï¼ˆamountï¼‰æ˜¯ 20 å…ƒï¼Œ
> å‡­è¯ç¼–å·ï¼ˆnonceï¼‰æ˜¯ 3ï¼Œ
> è¿™ä»½å‡­è¯å±äº åˆåŒå· #AABBCCï¼ˆcontractAddressï¼‰
> ç­¾åï¼šYushao
> ```

------

#### ğŸ” â€œç­¾ç½²å†…å®¹â€åˆ°åº•æ˜¯ä»€ä¹ˆï¼Ÿ

ç­¾ç½²çš„ä¸æ˜¯â€œæ–‡å­—â€ï¼Œè€Œæ˜¯ä¸€æ®µæ•°æ®çš„å“ˆå¸Œã€‚
 ä½†ä½ å¯ä»¥æŠŠå®ƒç†è§£æˆæ˜¯å¯¹è¿™å¥è¯ç­¾åï¼š

> â€œå¦‚æœè¿™å¼ å•æ®ï¼ˆä½™é¢=20å…ƒï¼‰æ˜¯æˆ‘ç­¾çš„ï¼Œé‚£æˆ‘ç¡®å®æˆæƒæ”¯ä»˜è¿™ä¹ˆå¤šã€‚â€

è€æ¿æ”¶åˆ°åï¼Œä»–è‡ªå·±ä¸èƒ½æ”¹æˆâ€œä½™é¢=30å…ƒâ€ï¼Œå› ä¸ºé‚£ä¼šå¯¼è‡´ç­¾åéªŒè¯å¤±è´¥ã€‚

âœ… **ç­¾åè¯æ˜äº†ä½ ç¡®å®è¯´è¿‡è¿™å¥è¯ã€‚**

------

#### ğŸ’¡ ç¬¬ä¸‰æ­¥ï¼šé€šé“ç»“ç®—ï¼ˆä¸Šé“¾ä¸€æ¬¡ï¼‰

å½“ä½ å–å®Œæœ€åä¸€æ¯ã€æˆ–è€…ä¸æƒ³ç»§ç»­å–äº†ï¼Œ
 è€æ¿æŠŠä½ æœ€åä¸€å¼ â€œç­¾åå‡­è¯â€æ‹¿å»é“¾ä¸Šï¼š

> ã€Œé“¾ä¸Šåˆçº¦ï¼Œè¯·æ ¹æ®è¿™ä»½ç­¾åæ”¯ä»˜æˆ‘ 50 å…ƒã€‚ã€

åˆçº¦éªŒè¯ç­¾åç¡®å®æ˜¯ä½ çš„ â†’ ç»™è€æ¿è½¬ 50 å…ƒ â†’ æŠŠå‰©ä¸‹çš„ 50 å…ƒè¿˜ä½ ã€‚
 æ•´ä¸ªè¿‡ç¨‹ä¸­ï¼Œ**åªæœ‰ä¸¤æ¬¡ä¸Šé“¾**ï¼š

1. å¼€é€šé€šé“ï¼ˆå­˜é’±ï¼‰
2. å…³é—­é€šé“ï¼ˆç»“ç®—ï¼‰

ä¸­é—´é‚£ 10 æ¬¡ç¦»çº¿ç­¾åéƒ½æ²¡èŠ± Gasã€‚

### ï¼ˆäºŒï¼‰å®˜ç½‘çš„ä¾‹å­

ä¾‹å­ä¸­ä½¿ç”¨åŠ å¯†ç­¾åï¼Œä½¿ä»¥å¤ªå¸åœ¨åŒä¸€å½“äº‹äººä¹‹é—´çš„é‡å¤è½¬ç§»å˜å¾—å®‰å…¨ã€å³æ—¶ï¼Œå¹¶ä¸”æ²¡æœ‰äº¤æ˜“è´¹ç”¨ã€‚ 

Aliceæƒ³å‘é€ä¸€äº›ä»¥å¤ªç»™Bobï¼Œ å³Aliceæ˜¯å‘é€æ–¹ï¼ŒBobæ˜¯æ¥æ”¶æ–¹ã€‚

Alice åªéœ€è¦åœ¨é“¾ä¸‹å‘é€ç»è¿‡åŠ å¯†ç­¾åçš„ä¿¡æ¯ (ä¾‹å¦‚é€šè¿‡ç”µå­é‚®ä»¶)ç»™Bobï¼Œå®ƒç±»ä¼¼äºå†™æ”¯ç¥¨ã€‚

Aliceå’ŒBobä½¿ç”¨ç­¾åæ¥æˆæƒäº¤æ˜“ï¼Œè¿™åœ¨ä»¥å¤ªåŠçš„æ™ºèƒ½åˆçº¦ä¸­æ˜¯å¯ä»¥å®ç°çš„ã€‚ Aliceå°†å»ºç«‹ä¸€ä¸ªç®€å•çš„æ™ºèƒ½åˆçº¦ï¼Œè®©å¥¹ä¼ è¾“ä»¥å¤ªå¸ï¼Œä½†å¥¹ä¸ä¼šè‡ªå·±è°ƒç”¨ä¸€ä¸ªå‡½æ•°æ¥å¯åŠ¨ä»˜æ¬¾ï¼Œ è€Œæ˜¯è®©Bobæ¥åšï¼Œä»è€Œæ”¯ä»˜äº¤æ˜“è´¹ç”¨ã€‚

è¯¥åˆçº¦å°†æŒ‰ä»¥ä¸‹æ–¹å¼è¿ä½œï¼š

- Aliceéƒ¨ç½²äº† ReceiverPays åˆçº¦ï¼Œé™„åŠ äº†è¶³å¤Ÿçš„ä»¥å¤ªå¸æ¥æ”¯ä»˜å°†è¦è¿›è¡Œçš„ä»˜æ¬¾ã€‚


- Aliceé€šè¿‡ç”¨å¥¹çš„ç§é’¥ç­¾ç½²ä¸€ä¸ªæ¶ˆæ¯æ¥æˆæƒä»˜æ¬¾ã€‚


- Aliceå°†ç»è¿‡åŠ å¯†ç­¾åçš„ä¿¡æ¯å‘é€ç»™Bobã€‚è¯¥ä¿¡æ¯ä¸éœ€è¦ä¿å¯†ï¼ˆåé¢ä¼šè§£é‡Šï¼‰ï¼Œè€Œä¸”å‘é€æœºåˆ¶ä¹Ÿä¸é‡è¦ã€‚


- Bobé€šè¿‡å‘æ™ºèƒ½åˆçº¦å‘é€ç­¾åçš„ä¿¡æ¯æ¥ç´¢å–ä»–çš„ä»˜æ¬¾ï¼Œåˆçº¦éªŒè¯äº†ä¿¡æ¯çš„çœŸå®æ€§ï¼Œç„¶åé‡Šæ”¾èµ„é‡‘ã€‚

#### åˆ›å»ºç­¾å

Aliceä¸éœ€è¦ä¸ä»¥å¤ªåŠç½‘ç»œäº¤äº’æ¥ç­¾ç½²äº¤æ˜“ï¼Œè¿™ä¸ªè¿‡ç¨‹æ˜¯å®Œå…¨ç¦»çº¿çš„ã€‚ æˆ‘ä»¬å°†ä½¿ç”¨ [web3.js](https://github.com/web3/web3.js) å’Œ [MetaMask](https://metamask.io/) åœ¨æµè§ˆå™¨ä¸­ç­¾ç½²ä¿¡æ¯ã€‚

```js
/// å…ˆè¿›è¡Œå“ˆå¸Œè¿ç®—ä½¿äº‹æƒ…å˜å¾—æ›´å®¹æ˜“
var hash = web3.utils.sha3("message to sign");
web3.eth.personal.sign(hash, web3.eth.defaultAccount, function () { console.log("Signed"); });
/// web3.eth.personal.sign æŠŠä¿¡æ¯çš„é•¿åº¦åŠ åˆ°ç­¾åæ•°æ®ä¸­ã€‚ ï¼ˆè¿™é‡Œæ˜¯ web3.eth.defaultAccountï¼‰å¯¹ hash è¿›è¡Œç­¾åã€‚personal.sign æ˜¯ JSON-RPC personal_sign çš„å°è£…ã€‚ç­¾åä¼šè¿”å›ä¸€ä¸ª signatureï¼ˆé€šå¸¸åœ¨ callback çš„å‚æ•°ä¸­ï¼‰ï¼Œè¿™é‡Œç¤ºä¾‹åªæ‰“å° â€œSignedâ€ã€‚
```

#### ç­¾ç½²å†…å®¹

å¯¹äºå±¥è¡Œä»˜æ¬¾çš„åˆçº¦ï¼Œç­¾ç½²çš„ä¿¡æ¯å¿…é¡»åŒ…æ‹¬ï¼š

> 1. æ”¶ä»¶äººçš„é’±åŒ…åœ°å€ã€‚
> 2. è¦è½¬ç§»çš„é‡‘é¢ã€‚
> 3. é‡æ”¾æ”»å‡»çš„ä¿æŠ¤ã€‚

é‡æ”¾æ”»å‡»æ˜¯æŒ‡ä¸€ä¸ªå·²ç­¾ç½²çš„ä¿¡æ¯è¢«é‡å¤ä½¿ç”¨ï¼Œä»¥è·å¾—å¯¹ç¬¬äºŒæ¬¡äº¤æ˜“çš„æˆæƒã€‚ ä¸ºäº†é¿å…é‡æ”¾æ”»å‡»ï¼Œæˆ‘ä»¬ä½¿ç”¨ä¸ä»¥å¤ªåŠäº¤æ˜“æœ¬èº«ç›¸åŒçš„æŠ€æœ¯ï¼Œ å³æ‰€è°“çš„nonceï¼Œå®ƒæ˜¯ä¸€ä¸ªè´¦æˆ·å‘é€çš„äº¤æ˜“æ•°é‡ã€‚ æ™ºèƒ½åˆçº¦ä¼šæ£€æŸ¥ä¸€ä¸ªnonceæ˜¯å¦è¢«å¤šæ¬¡ä½¿ç”¨ã€‚

å¦ä¸€ç§ç±»å‹çš„é‡æ”¾æ”»å‡»å¯èƒ½å‘ç”Ÿåœ¨æ‰€æœ‰è€…éƒ¨ç½² `ReceiverPays` åˆçº¦æ—¶ï¼Œ å…ˆè¿›è¡Œäº†ä¸€äº›æ”¯ä»˜ï¼Œç„¶åé”€æ¯è¯¥åˆçº¦ã€‚åæ¥ï¼Œ ä»–ä»¬å†³å®šå†æ¬¡éƒ¨ç½² `RecipientPays` åˆçº¦ï¼Œ ä½†æ–°çš„åˆçº¦ä¸çŸ¥é“ä»¥å‰åˆçº¦ä¸­ä½¿ç”¨çš„noncesï¼Œæ‰€ä»¥æ”»å‡»è€…å¯ä»¥å†æ¬¡ä½¿ç”¨æ—§çš„ä¿¡æ¯ã€‚

#### ç»„è£…å‚æ•°

æ—¢ç„¶æˆ‘ä»¬å·²ç»ç¡®å®šäº†è¦åœ¨ç­¾åä¿¡æ¯ä¸­åŒ…å«å“ªäº›ä¿¡æ¯ï¼Œ æˆ‘ä»¬å‡†å¤‡æŠŠä¿¡æ¯æ”¾åœ¨ä¸€èµ·ï¼Œè¿›è¡Œå“ˆå¸Œè¿ç®—ï¼Œç„¶åç­¾åã€‚ ç®€å•èµ·è§ï¼Œæˆ‘ä»¬æŠŠæ•°æ®è¿æ¥èµ·æ¥ã€‚ [ethereumjs-abi](https://github.com/ethereumjs/ethereumjs-abi) åº“æä¾›äº†ä¸€ä¸ªåä¸º `soliditySHA3` çš„å‡½æ•°ï¼Œ æ¨¡ä»¿Solidityçš„ `keccak256` å‡½æ•°åº”ç”¨äºä½¿ç”¨ `abi.encodePacked` ç¼–ç çš„å‚æ•°çš„è¡Œä¸ºã€‚ è¿™é‡Œæœ‰ä¸€ä¸ªJavaScriptå‡½æ•°ï¼Œä¸º `ReceiverPays` çš„ä¾‹å­åˆ›å»ºäº†é€‚å½“çš„ç­¾åã€‚

```solidity
// recipientï¼Œ æ˜¯åº”è¯¥è¢«æ”¯ä»˜çš„åœ°å€ã€‚
// amountï¼Œå•ä½æ˜¯ wei, æŒ‡å®šåº”è¯¥å‘é€å¤šå°‘etherã€‚
// nonceï¼Œ å¯ä»¥æ˜¯ä»»ä½•å”¯ä¸€çš„æ•°å­—ï¼Œä»¥é˜²æ­¢é‡æ”¾æ”»å‡»ã€‚
// contractAddressï¼Œ ç”¨äºé˜²æ­¢è·¨åˆçº¦çš„é‡æ”¾æ”»å‡»ã€‚
function signPayment(recipient, amount, nonce, contractAddress, callback) {
    var hash = "0x" + abi.soliditySHA3(
        ["address", "uint256", "uint256", "address"],
        [recipient, amount, nonce, contractAddress]
    ).toString("hex");

    web3.eth.personal.sign(hash, web3.eth.defaultAccount, callback);
}
```

> `function signPayment(recipient, amount, nonce, contractAddress, callback) {`
>  å®šä¹‰ä¸€ä¸ªç”¨äºç”Ÿæˆå¹¶ç­¾åâ€œæ”¯ä»˜å‡­è¯â€çš„å‡½æ•°ï¼Œæœ€åé€šè¿‡ `callback` è¿”å›ç­¾åï¼ˆæˆ–é”™è¯¯ï¼‰ã€‚
>
> `var hash = "0x" + abi.soliditySHA3( ... ).toString("hex");`
>  è¿™è¡Œæ˜¯å…³é”®ï¼Œåˆ†è§£å¦‚ä¸‹ï¼š
>
> - `abi.soliditySHA3([...types...], [...values...])`ï¼š
>    ä½œç”¨æ˜¯æŒ‰ **Solidity çš„ `abi.encodePacked` ç¼–ç è§„åˆ™** å°†å€¼æ‰“åŒ…ï¼Œç„¶åå¯¹æ‰“åŒ…åçš„å­—èŠ‚ä¸²åš keccak256ï¼ˆsolidity ä¸­çš„ `keccak256(abi.encodePacked(...))`ï¼‰ã€‚å®ƒæ¨¡ä»¿ Solidity ä¸­çš„è¡Œä¸ºï¼Œç¡®ä¿ JS ç«¯ç”Ÿæˆçš„å“ˆå¸Œä¸ Solidity åˆçº¦å†…é¢„æœŸä¸€è‡´ã€‚
> - `.toString("hex")`ï¼šæŠŠ `abi.soliditySHA3` è¿”å›çš„ Buffer æˆ–äºŒè¿›åˆ¶ç»“æœè½¬ä¸º hex å­—ç¬¦ä¸²ï¼ˆä¸å¸¦ `0x` å‰ç¼€ï¼‰ã€‚`"0x" + ...`ï¼šåœ¨ä»¥å¤ªåŠç”Ÿæ€ä¸­ï¼Œåå…­è¿›åˆ¶å­—ç¬¦ä¸²é€šå¸¸å¸¦ `0x` å‰ç¼€ï¼Œæ‰€ä»¥è¿™é‡ŒåŠ ä¸Š `0x`ï¼Œå½¢æˆæ ‡å‡†çš„ hex hash å­—ç¬¦ä¸²ï¼ˆä¾‹å¦‚ `0xabc123...`ï¼‰ã€‚
> - `personal.sign` ä¼š**åœ¨ç­¾åå‰å†æ¬¡åŠ ä¸Š Ethereum æ¶ˆæ¯å‰ç¼€**ï¼ˆ`\x19Ethereum Signed Message:\n${length}`ï¼‰å¹¶å°† `length` è®¾ç½®ä¸ºä½ ä¼ å…¥ `hash` çš„é•¿åº¦ï¼ˆå¦‚æœä½ ä¼ å…¥çš„æ˜¯ 32 å­—èŠ‚ hashï¼Œè¿™ä¸ª length æ˜¯ 32ï¼Œç­¾åçš„æ˜¯å‰ç¼€+32å­—èŠ‚å†…å®¹ï¼‰ã€‚å› æ­¤ï¼Œç­¾åè€…å®é™…ç­¾çš„æ˜¯ `prefixed(hash)`ï¼Œä¸æ˜¯â€œè£¸å“ˆå¸Œâ€ã€‚è¿™å¯¼è‡´åœ¨åˆçº¦é‡Œç›´æ¥ç”¨ `ecrecover` å»éªŒè¯ `keccak256(abi.encodePacked(...))` çš„åŸå§‹å“ˆå¸Œ **ä¸ä¼š**ä¸ `personal.sign` çš„ç­¾åç›´æ¥å¯¹åº”ï¼Œé™¤éä½ åœ¨åˆçº¦é‡Œä¹ŸåšåŒæ ·çš„å‰ç¼€å¤„ç†ï¼ˆå³ç”¨ `keccak256("\x19Ethereum Signed Message:\n32" + hash)`ï¼‰æ¥é‡å»ºç­¾åæ—¶çš„æ¶ˆæ¯æ‘˜è¦ã€‚æˆ–è€…ï¼Œä½ å¯ä»¥ç”¨å®¢æˆ·ç«¯ç­¾ `signTypedData`ï¼ˆEIP-712ï¼‰æ¥è·å¾—å¯è¢«åˆçº¦æ›´æ–¹ä¾¿æ ¡éªŒçš„ç»“æ„åŒ–ç­¾åã€‚
> - callback å‚æ•°é€šå¸¸æ˜¯ `(err, signature) => { ... }`ï¼Œç­¾åå­—ç¬¦ä¸²æ ¼å¼é€šå¸¸æ˜¯ 65 å­—èŠ‚å¹¶ç¼–ç ä¸º hexï¼ˆ`r` + `s` + `v`ï¼‰ã€‚

#### åœ¨Solidityä¸­æ¢å¤ä¿¡æ¯ç­¾åè€…

web3.js äº§ç”Ÿçš„ç­¾åæ˜¯ `r`, `s` å’Œ `v` çš„æ‹¼æ¥çš„ï¼Œ æ‰€ä»¥ç¬¬ä¸€æ­¥æ˜¯æŠŠè¿™äº›å‚æ•°åˆ†å¼€ã€‚æ‚¨å¯ä»¥åœ¨å®¢æˆ·ç«¯è¿™æ ·åšï¼Œ ä½†åœ¨æ™ºèƒ½åˆçº¦å†…è¿™æ ·åšæ„å‘³ç€ä½ åªéœ€è¦å‘é€ä¸€ä¸ªç­¾åå‚æ•°è€Œä¸æ˜¯ä¸‰ä¸ªã€‚ å°†ä¸€ä¸ªå­—èŠ‚æ•°ç»„åˆ†å‰²æˆå®ƒçš„ç»„æˆéƒ¨åˆ†æ˜¯å¾ˆéº»çƒ¦çš„ï¼Œ æ‰€ä»¥æˆ‘ä»¬åœ¨ `splitSignature` å‡½æ•°ä¸­ä½¿ç”¨ [inline assembly](https://docs.soliditylang.org/zh-cn/v0.8.23/assembly.html) å®Œæˆè¿™é¡¹å·¥ä½œï¼ˆæœ¬èŠ‚æœ«å°¾çš„å®Œæ•´åˆçº¦ä¸­çš„ç¬¬ä¸‰ä¸ªå‡½æ•°ï¼‰ã€‚

#### ä»£ç 

```solidity
// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0 <0.9.0;
// è¿™å°†æŠ¥å‘Šä¸€ä¸ªç”±äºåºŸå¼ƒçš„ selfdestruct è€Œäº§ç”Ÿçš„è­¦å‘Š
contract ReceiverPays {
    address owner = msg.sender;

    mapping(uint256 => bool) usedNonces;

    constructor() payable {}

    function claimPayment(uint256 amount, uint256 nonce, bytes memory signature) external {
        require(!usedNonces[nonce]);
        usedNonces[nonce] = true;

        // è¿™å°†é‡æ–°åˆ›å»ºåœ¨å®¢æˆ·ç«¯ä¸Šç­¾åçš„ä¿¡æ¯ã€‚
        bytes32 message = prefixed(keccak256(abi.encodePacked(msg.sender, amount, nonce, this)));

        require(recoverSigner(message, signature) == owner);

        payable(msg.sender).transfer(amount);
    }

    /// é”€æ¯åˆçº¦å¹¶æ”¶å›å‰©ä½™çš„èµ„é‡‘ã€‚
    function shutdown() external {
        require(msg.sender == owner);
        selfdestruct(payable(msg.sender));
    }

    /// ç­¾åæ–¹æ³•ã€‚
    function splitSignature(bytes memory sig)
        internal
        pure
        returns (uint8 v, bytes32 r, bytes32 s)
    {
        require(sig.length == 65);

        assembly {
            // å‰32ä¸ªå­—èŠ‚ï¼Œåœ¨é•¿åº¦å‰ç¼€ä¹‹åã€‚
            r := mload(add(sig, 32))
            // ç¬¬äºŒä¸ª32å­—èŠ‚ã€‚
            s := mload(add(sig, 64))
            // æœ€åä¸€ä¸ªå­—èŠ‚ï¼ˆä¸‹ä¸€ä¸ª32å­—èŠ‚çš„ç¬¬ä¸€ä¸ªå­—èŠ‚ï¼‰ã€‚
            v := byte(0, mload(add(sig, 96)))
        }

        return (v, r, s);
    }

    function recoverSigner(bytes32 message, bytes memory sig)
        internal
        pure
        returns (address)
    {
        (uint8 v, bytes32 r, bytes32 s) = splitSignature(sig);

        return ecrecover(message, v, r, s);
    }

    /// æ„å»ºä¸€ä¸ªå‰ç¼€å“ˆå¸Œå€¼ï¼Œä»¥æ¨¡ä»¿ eth_sign çš„è¡Œä¸ºã€‚
    function prefixed(bytes32 hash) internal pure returns (bytes32) {
        return keccak256(abi.encodePacked("\x19Ethereum Signed Message:\n32", hash));
    }
}
```

